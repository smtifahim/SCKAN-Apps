# Query to extract hierarchical anatomical locations of neuron soma, axon location, and axon terminal
# for SCKAN neurons. The query retrieves up to 4 levels of hierarchy for the soma location,
# the type of axon location, and the target organ and system of the axon terminal
# Updated on November 15, 2025. Added Systems hierarchy. Using rdf-star. -Fahim Imam       


PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>

SELECT DISTINCT 
?Neuron_ID
?A_L1_ID ?A_L1      # Level 1: brain/spinal cord/ganglion
?A_L2_ID ?A_L2      # Level 2: nucleus of brain/ spinal cord segments/ kind of ganglion
?A_L3_ID ?A_L3      # Level 3: kind of brain nucleus, more specific DRG and spinal cord segments C/T/L/S
?A_ID ?A            # Level 4: specific region   
?C_ID ?C 
?C_Type_ID ?C_Type    # via location, type of via e.g., nerve
?B_ID ?B            # Terminal structure
?Target_Organ_IRI ?Target_Organ
{                
    # Filtering neuron by ID regex
    # FILTER (regex(str(?Neuron_ID), 'keast-1')).
    
    ?Neuron_ID rdfs:subClassOf+ ?sckan_neuron . #only consider SCKAN neurons
    VALUES ?sckan_neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }
    
     {
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_ID>> rdfs:label ?A.
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_ID>> ilxtr:hasRegion ?AX.

      Optional 
      {
          <<?Neuron_ID ilxtr:hasAxonLocation ?C_ID>> rdfs:label ?C.
          <<?Neuron_ID ilxtr:hasAxonLocation ?C_ID>> ilxtr:hasRegion ?CX.
           OPTIONAL
           {     
                ?CX (rdfs:subClassOf* | ilxtr:isPartOf*) ?C_Type_ID.
                ?C_Type_ID rdfs:label ?C_Type.
                FILTER (?C_Type in ('nerve', 'nerve plexus', 'nerve fiber', 'nerve root', 
                                    'ganglion', 'white matter')).
           }
      }
      <<?Neuron_ID ilxtr:hasAxonTerminalLocation ?B_ID>> rdfs:label ?B.
      <<?Neuron_ID  ?axon_terminal ?B_ID>> ilxtr:hasRegion ?BX.
      #?BR_X rdfs:label ?BR_Label.
    }

    UNION  # unionize the neurons with sensory terminals with sensory axons leading to sensory terminals

    {
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_ID>> rdfs:label ?A.
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_ID>> ilxtr:hasRegion ?AX.

    OPTIONAL 
      {
        <<?Neuron_ID ilxtr:hasAxonToSensoryTerminal ?C_ID>> rdfs:label ?C.
        <<?Neuron_ID ilxtr:hasAxonToSensoryTerminal ?C_ID>> ilxtr:hasRegion ?CX.
         OPTIONAL
         {
             ?CX (rdfs:subClassOf* | ilxtr:isPartOf*) ?C_Type_ID.
             ?C_Type_ID rdfs:label ?C_Type.
             FILTER (?C_Type in ('nerve', 'nerve plexus', 'nerve fiber', 'nerve root', 
                                 'ganglion', 'white matter')).
         }
      }
      <<?Neuron_ID ilxtr:hasAxonSensoryLocation ?B_ID>> rdfs:label ?B.
      <<?Neuron_ID  ?axon_terminal ?B_ID>> ilxtr:hasRegion ?BX.
    }
    
    
    # Hierarchical origin
    OPTIONAL
    {
        # Check if A is/part of brain, spinal cord, or ganglia
        ?AX (rdfs:subClassOf* | ilxtr:isPartOf*) ?A_L1_ID.
        ?A_L1_ID rdfs:label ?A_L1.
        FILTER (?A_L1 in ('brain', 
                          'spinal cord', 
                          'ganglion', 
                          'nerve plexus'))
    
    
        # Check the part of brain, spinal cord, or kind of ganglia
        OPTIONAL
        {
            ?AX (rdfs:subClassOf*) ?A_L2_ID.
            ?A_L2_ID rdfs:label ?A_L2.
            FILTER (?A_L2 in ( 'nucleus of brain',
                                'nucleus of spinal cord',
                                'spinal cord segment',
                                'gray matter of spinal cord',
                                'white matter of spinal cord', 
                                ## Types of ganglion
                                'cranial ganglion',
                                'thoracic ganglion',
                                'cervical ganglion',
                                'dorsal root ganglion',
                                'paravertebral ganglion',
                                'prevertebral ganglion'
                            ))
    
    
            OPTIONAL {
                        ?AX (rdfs:subClassOf* | ilxtr:isPartOf*) ?A_L3_ID.
                        ?A_L3_ID rdfs:label ?A_L3.
                        FILTER (?A_L3 in (## Types of nucleus
                                        'brainstem nucleus',
                                        # 'cranial nerve nucleus',
                                        'diencephalic nucleus',
                                        # 'hindbrain nucleus',
                                        'midbrain nucleus',
                                        'pretectal nucleus',
                                        'telencephalic nucleus',
                                        'trigeminal nucleus',        
                        
                                        ## Spinal Cord Segments
                                        'cervical subsegment of spinal cord', # need to inspect
                                        'thoracic subsegment of spinal cord',
                                        'lumbar subsegment of spinal cord',
                                        'sacral subsegment of spinal cord',
                        
                                        ## Types of Dorsal Root Ganglions
                                        'cervical dorsal root ganglion',
                                        'lumbar dorsal root ganglion',
                                        'thoracic dorsal root ganglion',
                                        'sacral dorsal root ganglion'
                                        )
                                )
                        }
        }
    }
   
    OPTIONAL 
        { 
                ?BX (rdfs:subClassOf* | ilxtr:isPartOf* ) ?Target_Organ_IRI.      
                ?Target_Organ_IRI rdfs:label ?Target_Organ.
                        
                FILTER (?Target_Organ in ( 'heart', 'ovary', 'brain', 'urethra', 'esophagus', 'spinal cord', 'skin of body', 'lung', 'liver', 
                                            'lower urinary tract', 'urinary tract', 'muscle organ','gallbladder', 'colon', 'descending colon','kidney', 'intestine',
                                            'large intestine' ,'small intestine', 'stomach', 'spleen', 'urinary bladder', 'oral gland', 'tongue', 'respiratory tract', 
                                            'lower respiratory tract', 'upper respiratory tract', 'digestive tract', 'male reproductive system', 'female reproductive system',
                                            'eye gland', 'endocrine gland', 'spinal cord', 'vagina', 'hemopoietic organ', 'skin of body', 'adrenal gland','buttock', 
                                            'penis', 'clitoris', 'prostate gland', 'seminal vesicle','pancreas','uterus', 'sweat gland', 'arterial system',
                                                    'scapular muscle', 'sphincter muscle', 'flexor muscle', 'muscle of shoulder', 'muscle organ',
                                            # added systems
                                            'respiratory system', 'digestive system', 'endocrine system', 'lymphoid system', 'sense organ', 'renal system', 'integumental system',
                                            'cardiovascular system'
                                          )
                        )
        }
}
ORDER BY ?Neuron_ID ?Target_Organ ?Target_System ?B ?A ?C
LIMIT 100000