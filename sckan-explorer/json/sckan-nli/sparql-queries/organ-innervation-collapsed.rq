# Query: Organ inervation query for SCKAN NLI.
## CQ4: How is sympathetic innervation supplied to an end organ? Show the origins, 
## terminations and routing for both pre- and post-ganglionic connections.
## Version: Nov 16, 2025. - Fahim Imam

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT 
?Neuron_Connected
?Neuron_Label
?Target_Neuron_Phenotype
?V1_ID 
?V1 
?V1_Collapsed
?V1_Type 
?V2_ID
?V2
?V2_Collapsed ?V2_Type
?IsSynapse
?Target_Organ

WHERE 
{
    # FILTER (?Neuron_Connected != ilxtr:aacar-5)
    # FILTER (?Neuron_Connected != ilxtr:aacar-6)
    
    # Specify the Target_Organ
    # FILTER (?Target_Organ = 'ovary').
   
    # Specify the ANS phenotype for the Target_Neuron
    # FILTER (?Target_Neuron_Phenotype = 'Parasympathetic Post-Ganglionic phenotype').

    # FILTER (REGEX (?Target_Neuron_Phenotype, 'Sympathetic')).
    ?Neuron_Connected rdfs:subClassOf+ ?sckan_neuron . #only consider SCKAN neurons
    VALUES ?sckan_neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }

    ?Target_Neuron ilxtr:hasNeuronalPhenotype/rdfs:label ?Target_Neuron_Phenotype.
    
    # Get the Final_Terminal of the Target Neuron.
    <<?Target_Neuron ilxtr:hasAxonTerminalLocation ?X>> ilxtr:hasRegion ?Final_Terminal.
    
    # ?Final_Terminal rdfs:label ?ft.
    
    # Final terminal of the Target Neuron must be a part of Target Organ.
    ?Final_Terminal   (rdfs:subClassOf* | ilxtr:isPartOf*)/rdfs:label ?Target_Organ.
    FILTER (?Target_Organ in ('heart', 'ovary', 'brain', 'urethra', 'esophagus', 'spinal cord', 'skin of body', 'lung', 'liver', 
                                'lower urinary tract', 'urinary tract', 'muscle organ','gallbladder', 'colon', 'descending colon','kidney', 'intestine',
                                'large intestine' ,'small intestine', 'stomach', 'spleen', 'urinary bladder', 'oral gland', 'tongue', 'respiratory tract', 
                                'lower respiratory tract', 'upper respiratory tract', 'digestive tract', 'male reproductive system', 'female reproductive system',
                                'eye gland', 'endocrine gland', 'spinal cord', 'vagina', 'hemopoietic organ', 'skin of body', 'adrenal gland','buttock', 
                                'penis', 'clitoris', 'prostate gland', 'seminal vesicle','pancreas','uterus', 'sweat gland', 'arterial system',
                                 'scapular muscle', 'sphincter muscle', 'flexor muscle', 'muscle of shoulder', 'muscle organ',
                                # added systems
                                'respiratory system', 'digestive system', 'endocrine system', 'lymphoid system', 'sense organ', 'renal system', 'integumental system',
                                'cardiovascular system')) # not good: 'heart''bladder'

    {
       ?Neuron_Connected ilxtr:hasForwardConnection* ?Target_Neuron.
    #    ?Neuron_Connected ?V1_Location_Type_IRI ?V1_ID.
    #    ?Neuron_Connected ?V2_Location_Type_IRI ?V2_ID.
    } 

    ?V1_ID ilxtr:hasNextNode{ilxtr:isConnectedBy ?Neuron_Connected} ?V2_ID.
    <<?Neuron_Connected ?V1_Location_Type_IRI ?V1_ID>> rdfs:label ?V1.
    <<?Neuron_Connected ?V2_Location_Type_IRI ?V2_ID>> rdfs:label ?V2.                         
    
    ?Neuron_Connected skos:prefLabel ?Neuron_Label.
    
    # Find the locational phenotypes for the V1 node
    ?V1_Location_Type_IRI rdfs:label ?V1_Type.
    
    # Find the locational phenotype for the V2 node
    ?V2_Location_Type_IRI rdfs:label ?V2_Type.
  
    # Filter out the generic hasConnectedLocation relation for the connected nodes.
    FILTER (ilxtr:hasConnectedLocation not in (?V1_Location_Type_IRI, ?V2_Location_Type_IRI))
    
    OPTIONAL 
    {
        ?Neuron_Connected ilxtr:hasForwardConnection/ilxtr:hasSomaLocation ?Synapse.
        FILTER (?V2_ID = ?Synapse)
        FILTER (?V2_Type = "hasAxonTerminalLocation")

    }
    BIND (IF (BOUND(?Synapse), "YES", "NO") AS ?IsSynapse)

    
    # Set IsSynapse to "Yes" if a synapse is detected, otherwise leave it as NO
    #BIND (IF (BOUND(?Synapse), "YES", "NO") AS ?IsSynapse)
 BIND(
    IF(?V1_Type = "hasSomaLocation",
        REPLACE(?Neuron_Label, " to .*", ""),
        ?V1)
    AS ?V1_Temp
)

BIND(
    IF(?V2_Type = "hasAxonTerminalLocation",
        REPLACE(?Neuron_Label, ".* to (the )?(.*?) via .*", "$2"),
        ?V2)
    AS ?V2_Temp
)

# Step 2: Extract hyphenated/en-dash/figure-dash/em-dash terms (preserve case)
BIND(
    REPLACE(?V1_Temp, ".*?\\b([^\\s]*[-–‒—][^\\s]*)\\b.*", "$1") AS ?Hyphen_V1
)

BIND(
    REPLACE(?V2_Temp, ".*?\\b([^\\s]*[-–‒—][^\\s]*)\\b.*", "$1") AS ?Hyphen_V2
)

# Step 3: Preserve original case entirely
BIND(
    IF(
        STRLEN(?Hyphen_V1) > 0
        && (
            CONTAINS(?V1_Temp, "-") ||
            CONTAINS(?V1_Temp, "–") ||
            CONTAINS(?V1_Temp, "‒") ||
            CONTAINS(?V1_Temp, "—")
        ),
        REPLACE(REPLACE(?V1_Temp, ?Hyphen_V1, "___hyph___"), "___hyph___", ?Hyphen_V1),
        ?V1_Temp
    ) AS ?V1_Lowered
)

BIND(
    IF(
        STRLEN(?Hyphen_V2) > 0
        && (
            CONTAINS(?V2_Temp, "-") ||
            CONTAINS(?V2_Temp, "–") ||
            CONTAINS(?V2_Temp, "‒") ||
            CONTAINS(?V2_Temp, "—")
        ),
        REPLACE(REPLACE(?V2_Temp, ?Hyphen_V2, "___hyph___"), "___hyph___", ?Hyphen_V2),
        ?V2_Temp
    ) AS ?V2_Lowered
)

# Step 4: Force OR to uppercase, except for certain organs
BIND(
    IF(
        ?Target_Organ IN ("colon", "heart", "urinary bladder", "spleen", "digestive system", 'rectum', 'small intestine',
                            "endocrine system", "lymphoid system", 'respiratory system', 'intestine', 
                            'large intestine', 'liver', 'uterus', 'pancreas', 'parasympathetic nervous system'),
        ?V1,   # Skip collapsing for these organs
        REPLACE(?V1_Lowered, "(?i)\\bor\\b", "OR")
    ) AS ?V1_Collapsed
)

BIND(
    IF(
        ?Target_Organ IN ( "colon", "heart", "urinary bladder", "spleen", 'large intestine', 'small intestine',
                            "digestive system", "endocrine system", "lymphoid system", 'pancreas',
                            'respiratory system', 'intestine', 'rectum', 'liver', 'uterus', 'parasympathetic nervous system'),
        ?V2,   # Skip collapsing for these organs
        REPLACE(?V2_Lowered, "(?i)\\bor\\b", "OR")
    ) AS ?V2_Collapsed
)

}
ORDER BY ?Neuron_Connected ?Target_Neuron_Phenotype ?Target_Organ ?V1 ?V2
Limit 100000