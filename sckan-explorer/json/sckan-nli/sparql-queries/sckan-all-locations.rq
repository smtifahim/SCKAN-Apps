# The query returns all the connected anatomical locations along with their synonyms based on the 
# locational phenotype relations in SCKAN for SCKAN Explorer and SCKAN NLI.
# Version: November 12, 2025. Using rdf-star.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX NIFRID: <http://uri.neuinfo.org/nif/nifstd/readable/>

SELECT DISTINCT
# ?Neuron_ID
?Connection_Type
?Location_IRI
?Location_Label
{
    # FILTER (lcase(?Location_Label) = 'nodose ganglion')
    # FILTER (REGEX(str(?Neuron_ID), 'keast-1')).

    VALUES ?SCKAN_Neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }
    ?Neuron_ID rdfs:subClassOf+ ?SCKAN_Neuron .

   <<?Neuron_ID ?Connection_Type ?Region_IRI>> ilxtr:hasRegion ?Location_IRI.
             
   # Consider rdfs:label or any of the synonyms for the Location_Label for the UI autocomplete search. 
   OPTIONAL{?Location_IRI (rdfs:label | NIFRID:synonym | oboInOwl:hasExactSynonym) ?Location_Label.}
}
ORDER BY ?Neuron_ID  DESC(?Location_IRI) ?Location_Label
LIMIT 50000