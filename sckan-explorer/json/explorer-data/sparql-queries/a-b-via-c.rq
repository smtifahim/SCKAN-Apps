# QUERY: This query is for loading the a-b-via-c results in sckan-explorer.
# Added ilxtr:suppliesTo relation for arterial structures to be considered as
# part of end organs. - August 29, 2024
# Added union to distinguish the axons leading to sensory terminals. - September 28, 2024
# Added more target organs to be considered for sckanner and sckan NLI. - November 12, 2025

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>

# Neuron populations where A projects to B via some Nerve C

SELECT DISTINCT 
?Neuron_ID 
?A_IRI ?A_Label 
?B_IRI ?B_Label 
?C_IRI ?C_Label 
# ?BR_X # ?BR_Label
?Target_Organ_IRI ?Target_Organ_Label
{                
    #FILTER (REGEX(str(?Neuron_ID), 'keast-1')).
    ##  FILTER (?Target_Organ_Label = 'digestive tract').
    ## FILTER (regex (str(?Neuron_ID), 'aacar')).
    
    VALUES ?SCKAN_Neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }
    ?Neuron_ID rdfs:subClassOf+ ?SCKAN_Neuron .
    VALUES ?axon_terminal  {ilxtr:hasAxonTerminalLocation  ilxtr:hasAxonSensoryLocation}.

    {
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_IRI>> rdfs:label ?A_Label.
      Optional {<<?Neuron_ID ilxtr:hasAxonLocation ?C_IRI>> rdfs:label ?C_Label.}
      <<?Neuron_ID ilxtr:hasAxonTerminalLocation ?B_IRI>> rdfs:label ?B_Label.
      <<?Neuron_ID  ?axon_terminal ?B_IRI>> ilxtr:hasRegion ?BR_X.
      #?BR_X rdfs:label ?BR_Label.
    }

    UNION  # unionize the neurons with sensory terminals with sensory axons leading to sensory terminals

    {
      <<?Neuron_ID ilxtr:hasSomaLocation ?A_IRI>> rdfs:label ?A_Label.
      Optional {<<?Neuron_ID ilxtr:hasAxonToSensoryTerminal ?C_IRI>> rdfs:label ?C_Label.}
      <<?Neuron_ID ilxtr:hasAxonSensoryLocation ?B_IRI>> rdfs:label ?B_Label.
      <<?Neuron_ID  ?axon_terminal ?B_IRI>> ilxtr:hasRegion ?BR_X.
    }

    OPTIONAL 
    { 
        ?BR_X rdfs:label ?BR_Label.
        ?BR_X (rdfs:subClassOf* | ilxtr:isPartOf*) ?Target_Organ_IRI.
        ?Target_Organ_IRI rdfs:label ?Target_Organ_Label
                
        FILTER (?Target_Organ_Label in ( 'heart', 'ovary', 'brain', 'urethra', 'esophagus', 'spinal cord', 'skin of body', 'lung', 'liver', 
                                    'lower urinary tract', 'urinary tract', 'muscle organ','gallbladder', 'colon', 'descending colon','kidney', 'intestine',
                                    'large intestine' ,'small intestine', 'stomach', 'spleen', 'urinary bladder', 'oral gland', 'tongue', 'respiratory tract', 
                                    'lower respiratory tract', 'upper respiratory tract', 'digestive tract', 'male reproductive system', 'female reproductive system',
                                    'eye gland', 'endocrine gland', 'spinal cord', 'vagina', 'hemopoietic organ', 'skin of body', 'adrenal gland','buttock', 
                                    'penis', 'clitoris', 'prostate gland', 'seminal vesicle','pancreas','uterus', 'sweat gland', 'arterial system',
				                            'scapular muscle', 'sphincter muscle', 'flexor muscle', 'muscle of shoulder', 'muscle organ',
                                    # added systems
                                    'respiratory system', 'digestive system', 'endocrine system', 'lymphoid system', 'sense organ', 'renal system', 'integumental system',
                                    'cardiovascular system')
              )
    }
}
ORDER BY ?Neuron_ID ?A_Label ?B_Label ?C_Label
LIMIT 120000