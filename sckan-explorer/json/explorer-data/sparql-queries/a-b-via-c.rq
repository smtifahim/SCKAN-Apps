# QUERY: This query is for loading the a-b-via-c results in sckan-explorer.
# Added ilxtr:suppliesTo relation for arterial structures to be considered as
# part of end organs. - August 29, 2024
# Added union to distinguish the axons leading to sensory terminals. - September 28, 2024

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>

# Neuron populations where A projects to B via some Nerve C

SELECT DISTINCT ?Neuron_ID ?A_IRI ?A_Label ?B_IRI ?B_Label ?C_IRI ?C_Label ?Target_Organ_IRI ?Target_Organ_Label
{                
    ## FILTER (REGEX(str(?Neuron_ID), 'liver')).
    ##  FILTER (?Target_Organ_Label = 'digestive tract').
    ## FILTER (regex (str(?Neuron_ID), 'aacar')).

    # ?Neuron_ID rdfs:subClassOf+  <http://uri.neuinfo.org/nif/nifstd/sao1417703748> .
    
    VALUES ?SCKAN_Neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }
    ?Neuron_ID rdfs:subClassOf* ?SCKAN_Neuron .

    {
      ?Neuron_ID ilxtr:hasSomaLocation ?A_IRI.
           ?A_IRI rdfs:label ?A_Label.
    
      OPTIONAL {?Neuron_ID ilxtr:hasAxonLocation ?C_IRI. 
                ?C_IRI rdfs:label ?C_Label.}
      
      ?Neuron_ID ilxtr:hasAxonTerminalLocation ?B_IRI.
                  ?B_IRI rdfs:label ?B_Label.
    }

    UNION  # unionize the neurons with sensory terminals with sensory axons leading to sensory terminals

    {
      ?Neuron_ID ilxtr:hasSomaLocation ?A_IRI.
              ?A_IRI rdfs:label ?A_Label.
    
      OPTIONAL {?Neuron_ID ilxtr:hasAxonToSensoryTerminal ?C_IRI. 
                      ?C_IRI rdfs:label ?C_Label.}
      
      ?Neuron_ID ilxtr:hasAxonSensoryLocation ?B_IRI.
              ?B_IRI rdfs:label ?B_Label.
    }

    OPTIONAL 
    { 
        ?B_IRI (rdfs:subClassOf* | ilxtr:isPartOf* | ilxtr:suppliesTo* | ilxtr:isPartOf*/ilxtr:surrounds*)/rdfs:label | rdfs:label ?Target_Organ_Label.
        ?Target_Organ_IRI rdfs:label ?Target_Organ_Label
                    
        FILTER (?Target_Organ_Label in ( 'heart', 'ovary', 'brain', 'urethra', 'esophagus', 'spinal cord', 'skin of body', 'lung', 'liver', 
                                        'lower urinary tract', 'urinary tract', 'muscle organ','gallbladder', 'colon', 'descending colon','kidney', 
                                        'large intestine' ,'small intestine', 'stomach', 'spleen', 'urinary bladder', 'oral gland', 'tongue', 'respiratory tract', 
                                        'lower respiratory tract', 'upper respiratory tract', 'digestive tract', 'eye gland', 'endocrine gland', 'spinal cord', 'vagina', 
                                        'hemopoietic organ', 'skin of body', 'adrenal gland','buttock', 'penis', 'clitoris', 'prostate gland', 'seminal vesicle',
                                        'pancreas','uterus', 'sweat gland')
               )
   }            
}
ORDER BY ?Neuron_ID ?A_Label ?B_Label ?C_Label
LIMIT 120000