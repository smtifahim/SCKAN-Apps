## Query: Axonal path partial order with locational phenotype and synapses.
## Version: November 13, 2025. Using rdf-star. For SCKAN NLI and Explorer. - Fahim Imam

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT 
?Neuron_Connected 
?V1_ID ?V1 ?V1_Type 
?V2_ID ?V2 ?V2_Type
?IsSynapse

WHERE
{
  # FILTER (regex(str(?Neuron_Connected),'keast-1' ))
  ?Neuron_Connected rdfs:subClassOf+ ?sckan_neuron . #only consider SCKAN neurons
  VALUES ?sckan_neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }

  OPTIONAL {?Neuron_Connected skos:prefLabel ?Neuron_Label} .
  
  <<?V1_ID ilxtr:hasNextNode ?V2_ID >> ilxtr:isConnectedBy ?Neuron_Connected.
  <<?Neuron_Connected ?V1_Phenotype ?V1_ID>> rdfs:label ?V1.
  <<?Neuron_Connected ?V2_Phenotype ?V2_ID>> rdfs:label ?V2.
  
    # Find the locational phenotypes for the V1 and V2 node
  ?V1_Phenotype rdfs:label ?V1_Type.
  ?V2_Phenotype rdfs:label ?V2_Type.

 # Check if ?V2_ID is associated with a synapse
    OPTIONAL 
    {
        ?Neuron_Connnected ilxtr:hasForwardConnection/ilxtr:hasSomaLocation ?Synapse.
        FILTER (?V2_ID = ?Synapse)
        FILTER (?V2_Type = "hasAxonTerminalLocation")
    }
    
    # Set IsSynapse to "Yes" if a synapse is detected, otherwise leave it as NO
    BIND (IF (BOUND(?Synapse), "YES", "NO") AS ?IsSynapse)
}
ORDER BY ?Neuron_Connected ?V1 ?V2
LIMIT 50000