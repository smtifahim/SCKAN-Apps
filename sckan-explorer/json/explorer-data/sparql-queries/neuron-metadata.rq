# This query returns metadata for each of the SCKAN neuron population for SCKAN explorer.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?Neuron_IRI ?Neuron_Label ?Neuron_Pref_Label
                (GROUP_CONCAT(DISTINCT ?Species_1; SEPARATOR=", ") AS ?Species)
                ?Sex
                (GROUP_CONCAT(DISTINCT ?Phenotype; SEPARATOR=", ") AS ?Phenotypes)
                (GROUP_CONCAT(DISTINCT ?Forward_Connection; SEPARATOR=", ") AS ?Forward_Connections)
                ?Alert
                ?Reference
WHERE
{
    # FILTER ( regex (LCase(?Phenotype), 'sensory phenotype' ))

    # ?Neuron_IRI rdfs:subClassOf+ SAO:1417703748.   # SAO Neuron IRI http://uri.neuinfo.org/nif/nifstd/sao1417703748
    VALUES ?SCKAN_Neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }
    ?Neuron_ID rdfs:subClassOf* ?SCKAN_Neuron .

    ## Only considering connection forming neuron populations
    ?Neuron_IRI ilxtr:hasSomaLocation ?A_IRI; 
                (ilxtr:hasAxonTerminalLocation | ilxtr:hasAxonSensoryLocation) ?B_IRI.
    
    OPTIONAL {?Neuron_IRI rdfs:label ?Neuron_Label.}
    OPTIONAL {?Neuron_IRI skos:prefLabel ?Neuron_Pref_Label.}
            
    OPTIONAL {?Neuron_IRI ilxtr:isObservedInSpecies/rdfs:label ?Species_1.}
    OPTIONAL {?Neuron_IRI ilxtr:hasPhenotypicSex/rdfs:label ?Sex.}
    OPTIONAL {?Neuron_IRI ilxtr:reference ?Reference.}
    OPTIONAL {?Neuron_IRI ilxtr:alertNote ?Alert.}
    OPTIONAL {?Neuron_IRI ilxtr:hasForwardConnection ?Forward_Connection.}
    OPTIONAL {?Neuron_IRI (ilxtr:hasNeuronalPhenotype | 
                            ilxtr:hasFunctionalCircuitRole |
                            ilxtr:hasCircuitRole |
                            ilxtr:hasProjection  
                           )/rdfs:label ?Phenotype.}
}
GROUP BY ?Neuron_IRI ?Neuron_Label ?Neuron_Pref_Label ?Sex ?Alert ?Reference
ORDER BY ?Neuron_IRI
#LIMIT 10000